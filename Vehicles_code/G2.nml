// --------------------------------------------------------------------------------G2

spriteset(ss_G2_purchase, "src/METRO/G2_buy_8bpp.png")		{tmpl_purchase()}  //Vásárlási nézet
spriteset(ss_G2a_cc, "src/METRO/G2a_cc_1x_8bpp.png")		{tmpl_1(1)}
alternative_sprites(ss_G2a_cc, ZOOM_LEVEL_IN_2X, BIT_DEPTH_8BPP, "src/METRO/G2a_cc_2x_8bpp.png") {tmpl_1(2)}
alternative_sprites(ss_G2a_cc, ZOOM_LEVEL_IN_4X, BIT_DEPTH_8BPP, "src/METRO/G2a_cc_4x_8bpp.png") {tmpl_1(4)}
spriteset(ss_G2b_cc, "src/METRO/G2b_cc_1x_8bpp.png")		{tmpl_1(1)}
alternative_sprites(ss_G2b_cc, ZOOM_LEVEL_IN_2X, BIT_DEPTH_8BPP, "src/METRO/G2b_cc_2x_8bpp.png") {tmpl_1(2)}
alternative_sprites(ss_G2b_cc, ZOOM_LEVEL_IN_4X, BIT_DEPTH_8BPP, "src/METRO/G2b_cc_4x_8bpp.png") {tmpl_1(4)}
spriteset(ss_G2c_cc, "src/METRO/G2c_cc_1x_8bpp.png")		{tmpl_1(1)}
alternative_sprites(ss_G2c_cc, ZOOM_LEVEL_IN_2X, BIT_DEPTH_8BPP, "src/METRO/G2c_cc_2x_8bpp.png") {tmpl_1(2)}
alternative_sprites(ss_G2c_cc, ZOOM_LEVEL_IN_4X, BIT_DEPTH_8BPP, "src/METRO/G2c_cc_4x_8bpp.png") {tmpl_1(4)}
spriteset(ss_G2a_bkv, "src/METRO/G2a_bkv_1x_8bpp.png")		{tmpl_1(1)}
alternative_sprites(ss_G2a_bkv, ZOOM_LEVEL_IN_2X, BIT_DEPTH_8BPP, "src/METRO/G2a_bkv_2x_8bpp.png") {tmpl_1(2)}
alternative_sprites(ss_G2a_bkv, ZOOM_LEVEL_IN_4X, BIT_DEPTH_8BPP, "src/METRO/G2a_bkv_4x_8bpp.png") {tmpl_1(4)}
spriteset(ss_G2b_bkv, "src/METRO/G2b_bkv_1x_8bpp.png")		{tmpl_1(1)}
alternative_sprites(ss_G2b_bkv, ZOOM_LEVEL_IN_2X, BIT_DEPTH_8BPP, "src/METRO/G2b_bkv_2x_8bpp.png") {tmpl_1(2)}
alternative_sprites(ss_G2b_bkv, ZOOM_LEVEL_IN_4X, BIT_DEPTH_8BPP, "src/METRO/G2b_bkv_4x_8bpp.png") {tmpl_1(4)}
spriteset(ss_G2c_bkv, "src/METRO/G2c_bkv_1x_8bpp.png")		{tmpl_1(1)}
alternative_sprites(ss_G2c_bkv, ZOOM_LEVEL_IN_2X, BIT_DEPTH_8BPP, "src/METRO/G2c_bkv_2x_8bpp.png") {tmpl_1(2)}
alternative_sprites(ss_G2c_bkv, ZOOM_LEVEL_IN_4X, BIT_DEPTH_8BPP, "src/METRO/G2c_bkv_4x_8bpp.png") {tmpl_1(4)}

switch (FEAT_TRAINS, SELF, sw_G2a_cc, position_in_articulated_veh ) {	//pozició csuklón belül
	1: ss_toldat;
	2: ss_toldat;
    ss_G2a_cc;
}
switch (FEAT_TRAINS, SELF, sw_G2b_cc, position_in_articulated_veh ) {	//pozició csuklón belül
	1: ss_toldat;
	2: ss_toldat;
    ss_G2b_cc;
}
switch (FEAT_TRAINS, SELF, sw_G2c_cc, position_in_articulated_veh ) {	//pozició csuklón belül
	1: ss_toldat;
	2: ss_toldat;
    ss_G2c_cc;
}
switch (FEAT_TRAINS, SELF, swp2_G2_cc, position_in_consist_from_end) {	//pozició hátulról
	2: sw_G2c_cc;
	sw_G2b_cc;
}
switch (FEAT_TRAINS, SELF, swp1_G2_cc, position_in_consist) {	//pozició
	0: sw_G2a_cc;
	swp2_G2_cc;
}

switch (FEAT_TRAINS, SELF, sw_G2a_bkv, position_in_articulated_veh ) {	//pozició csuklón belül
	1: ss_toldat;
	2: ss_toldat;
    ss_G2a_bkv;
}
switch (FEAT_TRAINS, SELF, sw_G2b_bkv, position_in_articulated_veh ) {	//pozició csuklón belül
	1: ss_toldat;
	2: ss_toldat;
    ss_G2b_bkv;
}
switch (FEAT_TRAINS, SELF, sw_G2c_bkv, position_in_articulated_veh ) { //pozició csuklón belül
	1: ss_toldat;
	2: ss_toldat;
    ss_G2c_bkv;
}
switch (FEAT_TRAINS, SELF, swp2_G2_bkv, position_in_consist_from_end) {	//pozició hátulról
	2: sw_G2c_bkv;
	sw_G2b_bkv;
}
switch (FEAT_TRAINS, SELF, swp1_G2_bkv, position_in_consist) {	//pozició
	0: sw_G2a_bkv;
	swp2_G2_bkv;
}

switch (FEAT_TRAINS, SELF, sw_G2, cargo_subtype) { //refit
	0: swp1_G2_cc;
	1: swp1_G2_bkv;
	CB_RESULT_NO_TEXT;
}
switch (FEAT_TRAINS, SELF, sw_G2_livery_text, cargo_subtype) {	//refitt szöveg
	0: return string(STR_COLOUR_SCHEME_CC);
	1: return string(STR_COLOUR_SCHEME_BUDAPEST);
	CB_RESULT_NO_TEXT;
}
switch (FEAT_TRAINS, SELF, sw_G2_articulated, extra_callback_info1) { //csuklósítás
    0: item_G2;
    1: item_G2;
    2: item_G2;
    CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}
switch (FEAT_TRAINS, SELF, sw_G2_length, position_in_articulated_veh) {	//modellhossz
	1: return 1;
	2: return 8;
    return 8;
}
switch (FEAT_TRAINS, SELF, sw_G2_capacity, position_in_articulated_veh) {	//kapacitás
	1: return 2;
	2: return 8;
    return 8;
}
switch(FEAT_TRAINS, SELF, sw_G2_start_stop, num_vehs_in_consist) {
    0..4: return string(STR_G2_CANNOT_START);
	return CB_RESULT_NO_TEXT;
}
switch(FEAT_TRAINS, SELF, sw_G2_can_attach_wagon, vehicle_type_id) {
    item_G2: return CB_RESULT_ATTACH_ALLOW;
	return string(STR_G2_CANNOT_ATTACH_OTHER);
}

item(FEAT_TRAINS, item_G2) {
	property {
        name:								string(STR_G2_NAME);
        climates_available:				bitmask(CLIMATE_TEMPERATE, CLIMATE_ARCTIC, CLIMATE_TROPICAL);
        introduction_date:				date(1983,06,1);
		track_type:                   		ELRL;
        model_life:						5;
        vehicle_life:						30;
        reliability_decay:				70;
        refittable_cargo_classes:		bitmask(CC_PASSENGERS);
        non_refittable_cargo_classes:	bitmask(CC_PIECE_GOODS, CC_EXPRESS, CC_REFRIGERATED);
        loading_speed:					120;
        sprite_id:							SPRITE_ID_NEW_TRAIN;
        speed:								90 km/h;
        misc_flags:						bitmask(TRAIN_FLAG_2CC, TRAIN_FLAG_SPRITE_STACK);
        refit_cost:						0;
        running_cost_base:				RUNNING_COST_ELECTRIC;
        power:								392 hp;
		extra_power_per_wagon:			392 hp;
        weight:							33 ton;
        cargo_capacity:					255;
		cargo_age_period:					74;
        visual_effect_and_powered :		VISUAL_EFFECT_ELECTRIC;
		dual_headed:          	        0;
        engine_class:                		ENGINE_CLASS_ELECTRIC;
        tractive_effort_coefficient:	0.349;
        air_drag_coefficient:			0.035;
//		variant_group:					item_METR;
    }
    graphics {
		default:                 		sw_G2;
		purchase:						ss_G2_purchase;
		articulated_part:        		sw_G2_articulated;
		length:                  		sw_G2_length;
		cargo_subtype_text: 			sw_G2_livery_text;
		cost_factor:					80 * parapuco;
		running_cost_factor:			30 * pararuco;
		cargo_capacity:				sw_G2_capacity;
		purchase_cargo_capacity :	sw_G2_capacity;
		additional_text: 				string(STR_G2_ADAT);
	   	start_stop:					sw_G2_start_stop;
		can_attach_wagon:				sw_G2_can_attach_wagon;

    }
}


// --------------------------------------------------------------------------------G2a End
// --
// --
